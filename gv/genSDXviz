#!/usr/bin/perl -w

use strict;

my(%styles) = (
    bgp => "red",
    arp => "blue",
    arp_v => "lightblue",
    default => "black",
    default_v => "green",
    b1_v => "cyan",
    c1_v => "gold",
    c2_v => "gold4"
    );
    

my($TMP) = "/tmp/sdxviz.dot";
my($PNG) = "/tmp/sdxviz.png";

my(%prev_cnt,%cookie2cnt);
my(%tag2style, %tag2cookies);

my $count = 0;
my ($cur_flow_file, $cur_port_file, $cur_dot_file, $cur_png_file);

usage() if scalar(@ARGV) != 3;

my ($config_file, $dot_template, $out_dir) = (@ARGV);

load_config();

system("mkdir -p $out_dir/flows $out_dir/ports $out_dir/dot $out_dir/png") == 0 or die "Failed to make directories";
system("rm -f $out_dir/flows/* $out_dir/ports/* $out_dir/dot/* $out_dir/png/*");

while (1) {
    $cur_flow_file = sprintf("$out_dir/flows/%03d.flow", $count);
    $cur_port_file = sprintf("$out_dir/ports/%03d.ports", $count);
    $cur_dot_file = sprintf("$out_dir/dot/%03d.dot", $count);
    $cur_png_file = sprintf("$out_dir/png/%03d.png", $count);

    load_stats_files();
    load_stats();
    gen_dot_file();
    system("dot -Tpng $cur_dot_file > $cur_png_file");
    system("ln $cur_png_file $out_dir/tmp");
    system("mv $out_dir/tmp $out_dir/cur.png");
    ++$count;
    sleep(1);
}

sub usage {
    print "usage: genSDXviz <config_file> <dot_template> <out_dir>\n";
    exit 1;
}

sub load_config {
    open(CONFIG, "< $config_file") or die "Can't read $config_file";
    while (<CONFIG>) {
	s/#.*//;
	next if /^\s*$/;
	my @fields = split(/\s+/);
	die "Wrong number of fields in: $_" if scalar(@fields) != 4;
	my $tag = "$fields[0]-$fields[1]";
	$tag2style{$tag} = $fields[2];
	@{$tag2cookies{$tag}} = split(/,/, $fields[3]);
    }
    close(CONFIG);
}

sub gen_dot_file {
    open(OUT,">$cur_dot_file") or die "Can't open $cur_dot_file";
    open(DOT, "<$dot_template") or die "Can't read $dot_template";
    while (<DOT>) {
	my $line = $_;
	my @tags = ();
	if ($line =~ s|;\s*//\s*(.*)||) {
	    my($tag) = $1;
	    $tag =~ s/ //g;
	    @tags = split(/,/, $tag);
	    chomp $line;
	    print OUT "$line " . style_string(@tags) . "\n";
	} else {
	    print OUT "$line";
	}
    }
    close(DOT);
    close(OUT);
}

# loads stats into flows and ports files
sub load_stats_files {
    system("( ofdpa_show_flows 1|sort -n; ofdpa_show_flows 2|sort -n; ofdpa_show_flows 3|sort -n; ofdpa_show_flows 4|sort -n ) > $cur_flow_file") == 0 or die "Can't run show flows script";
    my($dpid);
    foreach $dpid (1, 2, 3, 4) {
	system("ofdpa_show_port_stats $dpid |sort -n| sed \'s/^/dpid: $dpid, /\' >> $cur_port_file") == 0 or die "Can't run port stats script";
    }
}

sub load_stats {
    open(FLOW_STATS,"<$cur_flow_file") or die "Can't open $cur_flow_file";
    %prev_cnt = %cookie2cnt;
    while (<FLOW_STATS>) {
	if (/(\d+).*packet_count\":\s+(\d+)/) {
	    my ($cookie,$count) = ($1,$2);
	    next if $cookie == 0;
	    $cookie2cnt{$cookie} = $count;
	    $prev_cnt{$cookie} = 0 if !defined($prev_cnt{$cookie});
#	    my $diff = $cookie2cnt{$cookie} - $prev_cnt{$cookie};
#	    my $diff = cookie_diff($cookie);
#	    print "cookie $cookie, count $count, diff $diff\n";
	}
    }
    close(FLOW_STATS);

    open(PORT_STATS,"<$cur_port_file") or die "Can't open $cur_port_file";
    while (<PORT_STATS>) {
	if (/dpid: (\d+).*port_no.: (\d+).*rx_packets.: (\d+)/) {
	    my ($dpid,$cookie,$count) = ($1,$2,$3);
	    $cookie = "PORT_${dpid}_$cookie";
	    $cookie2cnt{$cookie} = $count;
	    $prev_cnt{$cookie} = 0 if !defined($prev_cnt{$cookie});
	}
    }
    close(PORT_STATS);
}

sub cookie_diff {
    my($cookie) = (@_);
    return 0 if !defined($prev_cnt{$cookie});
    return $cookie2cnt{$cookie} - $prev_cnt{$cookie};
}

sub style_string {
    my(@tags) = (@_);

    my($diff,$style_string) = (0, "style=invis");
    my($tag, $cookie);
    foreach $tag (@tags) {
	next if !defined $tag2cookies{$tag};
#	print "tag/cookies: $tag $tag2cookies{$tag}\n"; exit;
	foreach $cookie (@{$tag2cookies{$tag}}) {
	    $diff += cookie_diff($cookie);
	    next if $diff == 0;
	    die "No style for tag: $tag" if !defined($tag2style{$tag});
	    my $style = $tag2style{$tag};
	    die "No string for style: $style" if !defined($styles{$style});
	    $style_string = "color=$styles{$style}";
	    $style_string .= ",penwidth=2.0" if $diff > 100;
	    $style_string .= ",penwidth=4.0" if $diff > 50000;
#	    print "$diff\n" if $diff > 10;
	}
    }
    return "[$style_string]";
}
